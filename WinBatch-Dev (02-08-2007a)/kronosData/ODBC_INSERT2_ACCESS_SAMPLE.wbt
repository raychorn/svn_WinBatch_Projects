MyDialogFormat=`WWWDLGED,5.0`

MyDialogCaption=`Phone book`
MyDialogX=28
MyDialogY=43
MyDialogWidth=282
MyDialogHeight=104
MyDialogNumControls=8

MyDialog01=`99,9,51,DEFAULT,STATICTEXT,DEFAULT,"PHONE BOOK"`
MyDialog02=`108,33,96,DEFAULT,EDITBOX,Fname,""`
MyDialog03=`108,49,96,DEFAULT,EDITBOX,LName,""`
MyDialog04=`54,33,51,DEFAULT,STATICTEXT,DEFAULT,"First Name"`
MyDialog05=`54,49,51,DEFAULT,STATICTEXT,DEFAULT,"Last Name"`
MyDialog06=`108,65,96,DEFAULT,EDITBOX,Wkphone,""`
MyDialog07=`54,65,51,DEFAULT,STATICTEXT,DEFAULT,"Work Phone"`
MyDialog08=`100,84,51,DEFAULT,PUSHBUTTON,DEFAULT,"Ok",1`

ButtonPushed=Dialog("MyDialog")


AddExtender("wwodb32i.dll")
;INITIALIZE VARIABLES
MyDataSource="ContactDSN"
MyTableName="Contacts"
;THIS ALLOCATES A SQL ENVIRONMENT HANDLE
henv = qAllocEnv()

If henv == -1
  ;THIS RETURNS LAST CODE SET BY LAST SQL FUNCTION
  retcode = qLastCode()  
  Message("qAllocEnv failed", retcode)
  Exit
Endif


;ALLOCATES A SQL CONNECTION HANDLE - MAXIMUM 
;OF 10 OPEN CONNECTION HANDLES
hdbc = qAllocConnect(henv) 

If hdbc == -1
  retcode = qLastCode()
  Message("qAllocConnect failed", retcode)
  Exit
Endif

;CONNECTS TO THE "SAMPLE" DATA SOURCE (with the data source name 'ContactDSN')
retcode = qConnect(hdbc, MyDataSource, "", "") 

If (retcode != @qSuccess) && (retcode != @qSuccessInfo)
  Message("qConnect failed", retcode)
  Exit
Endif

;THIS ALLOCATES A SQL STATEMENT HANDLE
hstmt = qAllocStmt(hdbc)  

If hstmt == -1
  retcode = qLastCode()
  Message("qAllocStmt failed", retcode)
  Exit
Endif


;FILLS A RESULT SET WITH A LIST OF COLUMN NAMES IN THE SPECIFIED TABLE FROM "SAMPLE" DATA SOURCE
retcode = qColumns(hstmt, @qNull, @qNull, MyTableName, @qNull) 

If (retcode != @qSuccess) && (retcode != @qSuccessInfo)
  Message("qColumns failed", retcode)
  Exit
Endif

;BINDS A COLUMN IN A RESULT SET TO A VARIABLE THAT YOU NAME HERE
retcode = qBindCol(hstmt, 4, "colname", 80)  

If (retcode != @qSuccess) && (retcode != @qSuccessInfo)
 Message("qBindCol failed", retcode)
  Exit
Endif

columns = ""
While @TRUE
  retcode = qFetch(hstmt)

  If retcode == @qNoData Then Break

  If (retcode != @qSuccess) && (retcode != @qSuccessInfo)
    Message("qFetch failed", retcode)
    Exit
  Endif

  columns = StrCat(columns, colname,",")
EndWhile

;kill trailing comma
strend=strlen(columns)-1
columns=strsub(columns,1,strend)

;SQL_CLOSE - RESETS OR FREES A SQL STATEMENT HANDLE 
;(SET WITH QALLOC)
qFreeStmt(hstmt, 0)  
qFreeStmt(hstmt, 2)  ; SQL_UNBIND



;THE FOLLOWING EXECUTES AN SQL STATEMENT.
;THE COLUMNS BELOW ARE ACTUAL COLUMN NAMES IN YOUR DATA SOURCE, ;E.G.,"LASTNAME":

SQLCOMMAND1="INSERT INTO %mytablename%"
SQLCOMMAND2="(FirstName,LastName,WorkPhone) "
SQLCOMMAND3="VALUES('%fName%','%LName%','%WkPhone%')"
SQLCOMMAND=StrCat(SQLCOMMAND1,SQLCOMMAND2,SQLCOMMAND3);
retcode = qExecDirect(hstmt,SQLCOMMAND)

If (retcode != @qSuccess) && (retcode != @qSuccessInfo)
  err=qerror(hstmt,2)
  message(err,SQLCOMMAND)
  Exit
Endif

;SQL_DROP  - RESETS OR FREES AN SQL STATEMENT HANDLE
qFreeStmt(hstmt, 1)
;CLOSES A CONNECTION TO A DATA SOURCE
qDisconnect(hdbc)
;FREES AN SQL CONNECTION HANDLE
qFreeConnect(hdbc)
;FREES AN SQL ENVIRONMENT HANDLE
qFreeEnv(henv)

ver = qVersionInfo(0)
;DISPLAYS THE ODBC EXTENDER VERSION
Message("SQL Test (extender version %ver%)", "Done") 


exit



