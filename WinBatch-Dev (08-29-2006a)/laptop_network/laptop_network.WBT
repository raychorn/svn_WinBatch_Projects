;************************************************************************
; cf_encoder.wbt
; 
;************************************************************************

#DefineFunction GetWMIInfo()
	IntControl(73, 2,0,0,0)
	Locator = ObjectOpen("WbemScripting.SWbemLocator")
	Service = Locator.ConnectServer()
	Security = Service.Security_     
	Security.ImpersonationLevel = 3
	Class = "Win32_ComputerSystemProduct"
	Instance = Service.InstancesOf(Class)
	Enum = ObjectCollectionOpen(Instance)
	While 1
		Obj = ObjectCollectionNext(Enum)
		If Obj == 0 Then Break
		man=Obj.Vendor
		model=Obj.Name
		serial=Obj.IdentifyingNumber
	EndWhile
	
	ObjectCollectionClose(Enum)
	ObjectClose(Instance)
	ObjectClose(Security)
	ObjectClose(Service)
	ObjectClose(Locator)
	data=StrCat(man,@TAB,model,@TAB,serial)
	Return(data)
	
	:WBERRORHANDLER
	Error=LastError()
	Terminate(error!=3246,"Error %error%",StrCat(wberrorhandlerline,@CRLF,IntControl(34,error,0,0,0)))
	%wberrorhandlerassignment%="Not Found"
	IntControl(73, 2,0,0,0)
	Return
#EndFunction


;This UDF is sort of like the FileItemPath function in that it returns a
;tab delimited list of files.  

; This function returns a tab delimited list of files in a directory tree.
; Parameters
;       tree - always initialize to ""
;       dir  - directroy to start in
;       mask - file mask to search for, like "*.*"   "*.exe"  "*.txt" etc
;              no directory info allows in the mask
;       currentlevel - initialize to zero
;       maxlevel  - controls how many levels deep you wish to go.  TO
;                   go all the way down set to a large number like 9999
;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#DefineFunction FileItemRecursive(tree,dir,mask,currentlevel,maxlevel)
   ;DebugTrace(@ON,"xxxdebug.txt")
   If currentlevel>=maxlevel Then Return(tree)
   currentlevel=currentlevel+1

   DirChange(dir)
   dlist=ItemSort(DirItemize("*.*"),@TAB)
   dcount=ItemCount(dlist,@TAB)

   For xx=1 To dcount
      thisdir=ItemExtract(xx,dlist,@TAB)
      tree=FileItemRecursive(tree,thisdir,mask,currentlevel,maxlevel)
   Next

   If tree=="" Then tt=""
   Else tt=@TAB

   flist=FileItemPath(mask)
   If flist != ""
      flist=ItemSort(flist,@TAB)
      tree=StrCat(tree,tt,flist)
   EndIf

   DirChange("..")
   Return(tree)
#EndFunction


_wname = "cf_encoder v1.0"
wname = "%_wname% (c)1978-2006 Hierarchical Applications Limited, All Rights Reserved."

IntControl (81, GetTickCount(), 0, 0, 0)

_wname_ = StrReplace( StrReplace( _wname, ".", "_"), " ", "_")

const_Cr = Num2Char(13)

const_licenseKey_symbol = "licenseKey"
const_cfencoder_symbol = "cfencoder"

_myPath = FilePath( IntControl( 1004, 0, 0, 0, 0))

_iniPath = '%_myPath%%_wname_%.INI'

if FileExist(_iniPath) == @False
	IniWritePvt("Main", const_licenseKey_symbol, "", _iniPath)
	IniWritePvt("Main", const_cfencoder_symbol, "C:\JRun4\servers\cfusion\cfusion-ear\cfusion-war\WEB-INF\cfusion\bin\cfencode.exe", _iniPath)
endif
licenseKey = IniReadPvt ("Main", const_licenseKey_symbol, "", _iniPath)
cfencoderEXE = IniReadPvt ("Main", const_cfencoder_symbol, "", _iniPath)

;;;;;;;;;;;;;;;; Generic Initialization ;;;;;;;;;;;;;;;;;;;;
IntControl(1008,1,0,0,0)
IntControl(12,5,0,0,0)
;;;;;;;; Assorted Colors ;;;;;;;;;
DKBLUE="0,0,128"
BLUE="0,0,255"
LTGRAY="192,192,192"
GRAY="128,128,128"
DKGRAY="64,64,64"
GREEN="0,255,0"
RED="255,0,0"
BLACK="0,0,0"
WHITE="255,255,255"
YELLOW="255,255,0"

const_my_laptop_machine_id = "CND607062Y"

datax = GetWmiInfo()
;aa = AskItemlist("???",datax,@TAB,@UNSORTED,@SINGLE)

_licenseWarning = @FALSE

machineNum = ItemExtract( ItemCount( datax,@TAB), datax,@TAB)
if machineNum == const_my_laptop_machine_id
	for lki = 1 to ItemCount( datax,@TAB)
		_concat_char = ""
		if lki <> ItemCount( datax,@TAB) then _concat_char = "+"
		licenseKey = StrCat(licenseKey, StrTrim(ItemExtract( lki, datax,@TAB)), _concat_char) ; fake a license Key for now for testing...
	next
else
	if (StrLen(licenseKey) == 0)
		_licenseWarning = @TRUE
		Message( "WARNING", "You have selected a function that can only be performed AFTER you have obtained a Registration License Key for this product.%const_Cr%")
	endif
endif

if _licenseWarning == @FALSE

	If param0 > 1
		_srcFOLDER = param1
	Else
		flags=4|8
		_srcFOLDER = IniReadPvt ("Main", "_srcFOLDER", "c:\inetpub\wwwroot\", _iniPath)
		_srcFOLDER = AskDirectory("Source folder to encode from ?", "", _srcFOLDER,"Are you sure?",flags)
		IniWritePvt("Main", "_srcFOLDER", _srcFOLDER, _iniPath)
	Endif

	If param0 > 2
		_dstFOLDER = param2
	Else
		flags=1|2
		_dstFOLDER = IniReadPvt ("Main", "_dstFOLDER", "c:\inetpub\wwwroot\", _iniPath)
		_dstFOLDER = AskDirectory("Destination folder to encode to ?", "", _dstFOLDER,"Are you sure?",flags)
		IniWritePvt("Main", "_dstFOLDER", _dstFOLDER, _iniPath)
	Endif

endif

if FileExist( cfencoderEXE) == @False
	types = "EXE Files|*.exe"
	cfencoderEXE = AskFileName("Select the Cfencoder file", "C:\", types, "cfencode.exe", 1)
	IniWritePvt("Main", const_cfencoder_symbol, cfencoderEXE, _iniPath)
endif

srcFilesList = FileItemRecursive("",_srcFOLDER,"*.cf?",0,15)
allFilesList = FileItemRecursive("",_srcFOLDER,"*.*",0,15)

dstFilesList = FileItemRecursive("",_dstFOLDER,"*.*",0,15)
FileDelete(dstFilesList)

;aa = AskItemList("Files", srcFilesList, @tab, @sorted, @single, @false)
;aa = AskItemList("Files", allFilesList, @tab, @sorted, @single, @false)

Gosub PerformProcessingWithGUI

stats_total = stats_encoded + stats_copied
Message( "Run-Time Stats", "%stats_total% Files Processed. %stats_encoded% Files Encoded.  %stats_copied% Files Copied but not Encoded.")

exit


:PerformProcessingWithGUI
	bExit=1   
	bRegister=2

	BoxesUp("100,100,900,900",@NORMAL)

	rectutext="100,100,1000,900"
	rectTherm="200,350,800,650"
	rectButton="0,0,100,100"
	rectThermLine1="100,550,900,550"
	rectThermLine2="900,550,900,800"
	rectThermLine3="900,800,100,800"
	rectThermLine4="100,800,100,550"
	ThermPenWidth=20
	rectThermText1="100,50,1000,199"
	rectThermPercent="490,625,550,725"
	rectTopHalf="0,0,1000,210"
	rectBotHalf="0,210,1000,1000" 
	rectTitle="100,20,900,170"
	rectTitleText="10,10,790,140"
	TitleHeight=950
	rectNote="100,30,900,270"
	NoteHeight=400
	rectNoteText="70,200,950,800"
     
	rectnoteline1="0,0,1000,0"
	rectNoteline2="1000,1000,1000,0"
	rectNoteLine3="0,1000,1000,1000"
	rectNoteLine4="0,0,0,1000"
	notepenwidth=20
	rectnoteline1b="40,150,960,150"
	rectNoteline2b="960,840,960,150"
	rectNoteLine3b="40,840,960,840"
	rectNoteLine4b="40,150,40,840"
	notepenwidthb=10
	Thermid=3
	Noteid=4
	; Make main window look nice
	BoxColor(1,"128,0,0",1)
	BoxDrawRect(1,"0,0,1000,1000",2)
	BoxCaption(1,"Encoding files for ColdFusion MX")
             
	;;;;Do note box
	BoxNew(Noteid,rectNote,1)
	BoxColor(Noteid,LTGRAY,0)       ; Light Gray no gradient
	BoxDrawRect(Noteid,"",2)
	
	BoxTextFont(Noteid,"Arial",NoteHeight / 2,170,0)
	BoxTextColor(Noteid,RED)
	
	BoxPen(Noteid,WHITE,notepenwidth)
	boxdrawline(Noteid,rectNoteLine1)
	boxdrawline(Noteid,rectNoteLine4)
	BoxPen(Noteid,GRAY,notepenwidth)
	boxdrawline(Noteid,rectNoteLine2)
	boxdrawline(Noteid,rectNoteLine3)
	BoxPen(Noteid,WHITE,notepenwidthb)
	boxdrawline(Noteid,rectNoteLine2b)
	boxdrawline(Noteid,rectNoteLine3b)
	BoxPen(Noteid,GRAY,notepenwidthb)
	boxdrawline(Noteid,rectNoteLine1b)
	boxdrawline(Noteid,rectNoteLine4b)
	
	BoxDataTag(Noteid,"NOTE")
	BoxDrawText(Noteid,rectNoteText,"Initializing...",1,4)
             
	;Draw thermoneter outline
	BoxNew(Thermid,rectTherm,2)
	BoxUpdates(Thermid,0)
	BoxCaption(Thermid,"Name Goes Here")
	BoxColor(Thermid,LTGRAY,0)
	BoxDrawRect(Thermid,"",2)
	; Draw updating thermometer here
	; there are 3 virtual pixels per percent
	; we have to draw 3 boxes and some text...
	BoxDataTag(Thermid,"FRED")
	BoxCaption(Thermid,"Please wait. Processing Files...")
	
	n = ItemCount( allFilesList, @tab)

	if (StrLen(licenseKey) == 0)
		Message("Shareware Notice", "You are using a shareware version of this cf_encoder program. Please purchase a License to unlock the full version.")
	endif
             
	if _licenseWarning == @FALSE
		stats_encoded = 0
		stats_copied = 0

		fp = FilePath(_dstFOLDER)
		sp = FilePath(_srcFOLDER)
		
		for ii = 1 to n
			BoxDataClear(Thermid,"FRED")
			BoxUpdates(Thermid,0)
			BoxColor(Thermid,GREEN,0)
			xxx= 100+((800*ii)/n)
			per=(100.0*ii)/n
			per=Int(per)
			BoxDrawRect(Thermid,"104,554,%xxx%,796",2)
			BoxColor(Thermid,GRAY,0)
			BoxDrawRect(Thermid,"%xxx%,554,896,796",2)
			BoxPen(Thermid,BLACK,ThermPenWidth)
			boxdrawline(Thermid,rectThermLine1)
			boxdrawline(Thermid,rectThermLine2)
			boxdrawline(Thermid,rectThermLine3)
			boxdrawline(Thermid,rectThermLine4)
	
			s = ItemExtract( ii, allFilesList, @tab)
			t = StrReplace( s, sp, fp)
			_s = s
			k = ItemLocate( s, srcFilesList, @tab)
			if (k > 0)
				dp = FilePath(t)
				if (DirExist(dp) == @FALSE)
					DirMake(dp)
				endif
				RunShell(cfencoderEXE, '%s% %t% /q /v "2"', "", @NORMAL, @WAIT)
				s = StrCat("*",t)
				stats_encoded = stats_encoded + 1
			else
				_p = ""
				_n = ItemCount( t, "\")
				for zi = 1 to _n
					_p = StrCat(_p, ItemExtract( zi, t, "\"))
					if (zi > 1) && (zi < _n)
						if (DirExist(_p) == @FALSE)
							DirMake(_p)
						endif
					endif
					_p = StrCat(_p, "\")
				next
				ErrorMode(@OFF)
				bool = FileCopy( _s, t, @False)
				_err = LastError()
				ErrorMode(@CANCEL)
				; simply copy the file without encoding it...
				if ( (bool == @true) && (FileExist(t)) && (_err == 0) )
					stats_copied = stats_copied + 1
				else
					Message( "DEBUG", "ERROR: s = (%_s%), t = (%t%), _err = (%_err%)")
				endif
			endif
	
			if per<50 then BoxTextColor(Thermid,WHITE)
			BoxDrawText(Thermid,rectThermPercent,"%per%%%",0,0)
			BoxTextColor(Thermid,BLACK)
			BoxColor(Thermid,LTGRAY,0)
			BoxDrawText(Thermid,rectThermText1,"%ii%/%n% (%s%)",1,0)
			BoxDataClear(NoteId,"NOTE");;;
			BoxDrawText(Noteid,rectNoteText,"Encoding ColdFusion Files",1,4);;;
			BoxUpdates(Thermid,2)
	
		;	TimeDelay(Random(2.0))   ;Fake passage of time
		next

		BoxTextColor(Noteid,BLUE)        
		BoxDrawText(Noteid,rectNoteText,"Done !",1,4)
	else
		BoxTextColor(Noteid,BLUE)        
		BoxDrawText(Noteid,rectNoteText,"PLS Register to proceed.",1,4)
	endif

	if (StrLen(licenseKey) == 0)
		while 1
			BoxButtonDraw(1,bRegister,"&Register",  "550,860,700,930")
			BoxButtonDraw(1,bExit,    "E&xit",  "750,860,900,930")
	
			iBox = 0
			BoxButtonWait()
			while iBox == 0
				for x = 1 to 2
					if BoxButtonStat(1,x) then iBox = x
				next
			end while

			if iBox
				switch iBox
					case bExit
						exit
						break
					case bRegister
						gosub DoRegister
						break
				end switch
			endif
		end while
	endif

	if (StrLen(licenseKey) > 0)
		BoxDestroy(Thermid)
		BoxDestroy(Noteid)
	endif
Return



